plugins {
    id 'maven-publish'
    id 'java-gradle-plugin'
}

dependencies {
    compile gradleApi()
    // Compile only is to avoid the maven lookup of dependencies, we include them in the jar below
    compileOnly project(":plugin-model")
    compileOnly group: "net.contrapt.jvmcode", name: "jvmcode-model", version: jvmcode_version, changing: true

    testCompile gradleTestKit()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

publishing {
    repositories {
        maven {
            url new File("${project.rootDir}/out/m2/repo").toURI().toURL()
        }
    }

    publications {
        Plugin(MavenPublication) {
            from components.java
        }
    }
}

jar {
    from project(":plugin-model").sourceSets.main.output
    from { configurations.compileOnly.collect {
        (it.name.contains('jvmcode-model') || it.name.contains('kotlin-stdlib')) ? (it.isDirectory() ? it : zipTree(it)) : [] }
    }
}

task deploy(type: Copy, dependsOn: ['publish']) {
    from ("${project.buildDir}/resources/main") {
        include 'init.gradle'
    }
    into "${project.rootDir}/out/m2"
}

clean {
    doLast() {
        file("${project.rootDir}/out/m2").eachFileRecurse {
            it.delete()
        }
    }
}
